#!/usr/bin/perl -w

# Copyright (C) 2005 Apple Computer, Inc.  All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
#
# 1.  Redistributions of source code must retain the above copyright
#     notice, this list of conditions and the following disclaimer. 
# 2.  Redistributions in binary form must reproduce the above copyright
#     notice, this list of conditions and the following disclaimer in the
#     documentation and/or other materials provided with the distribution. 
# 3.  Neither the name of Apple Computer, Inc. ("Apple") nor the names of
#     its contributors may be used to endorse or promote products derived
#     from this software without specific prior written permission. 
#
# THIS SOFTWARE IS PROVIDED BY APPLE AND ITS CONTRIBUTORS "AS IS" AND ANY
# EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL APPLE OR ITS CONTRIBUTORS BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
# ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
# THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

# Script to run the Web Kit Open Source Project layout tests.

use strict;
use IPC::Open2;
use FindBin;
use Cwd;
use lib $FindBin::Bin;
use webkitdirs;

# Run all the tests passed in on the command line.
# If no tests are passed, find all the .svg files in the test directory.

# Run each text.
# Compare against the existing file xxx-expected.txt.
# If there is a mismatch, generate xxx-actual.txt and xxx-diffs.txt.

# At the end, report:
#   the number of tests that got the expected results
#   the number of tests that ran, but did not get the expected results
#   the number of tests that failed to run
#   the number of tests that were run but had no expected results to compare against

setConfiguration();
my $productDir = productDir();

chdirWebKit();

my $result = system "WebKitTools/Scripts/build-dumpkcanvastree", @ARGV;
exit $result if $result;

my $tool = "$productDir/DumpKCanvasTree";
die "can't find executable DumpKCanvasTree tool (looked in $productDir)\n" if !-x $tool;

checkSVGFrameworks();

my $workingDir = getcwd();
my $SVGSupportDirectory = "$workingDir/SVGSupport";
my $testDirectory = "$SVGSupportDirectory/layout-tests";
my $testResultsDirectory = "/tmp/layout-test-results";
my $testResults = "$testResultsDirectory/results.html";

$ENV{DYLD_FRAMEWORK_PATH} = $productDir;

my @tests;

my $findArguments = "\\( -name resources \\! -prune \\) -or -name '*.svg'";
my $foundTestName = 0;
for my $test (@ARGV) {
    next if $test =~ /^-/;
    $foundTestName = 1;
    $test =~ s/^$testDirectory\///;
    if ($test =~ /^\//) {
        print "can't run test outside $testDirectory\n";
    } elsif (-f "$testDirectory/$test") {
        if ($test !~ /\.(svg)$/) {
            print "test $test does not have an .svg extension\n";
        } else {
            push @tests, $test;
        }
    } elsif (-d "$testDirectory/$test") {
        push @tests, map { chomp; s-^$testDirectory/--; $_; } `find -Ls $testDirectory/$test $findArguments`;
    } else {
        print "test $test not found\n";
    }
}
if (!$foundTestName) {
    @tests = map { chomp; s-^$testDirectory/--; $_; } `find -Ls $testDirectory $findArguments`;
}

die "no tests to run\n" if !@tests;

my %counts;
my %tests;
my $count;

open2(\*IN, \*OUT, $tool, "-") or die;

$| = 1;

for my $test (@tests) {
    next if $test eq 'results.html';
    
    my $base = $test;
    $base =~ s/\.(svg)$//;
    
    print "running $base test";
    
    my $result;

    print OUT "$testDirectory/$test\n";

    my $actual = "";
    while (<IN>) {
        last if /#EOF$/;
        $actual .= $_;
    }
    
    my $expected;
    if (open EXPECTED, "<", "$testDirectory/$base-expected.txt") {
        $expected = "";
        while (<EXPECTED>) {
            $expected .= $_;
        }
        close EXPECTED;
    }
    if (!defined $expected) {
        print " -> new test\n";        
        $result = "new";
        open EXPECTED, ">", "$testDirectory/$base-expected.txt" or die "could not create $testDirectory/$base-expected.txt\n";
        print EXPECTED $actual;
        close EXPECTED;
        unlink "$testResultsDirectory/$base-actual.txt";
        unlink "$testResultsDirectory/$base-diffs.txt";
    } elsif ($actual eq $expected) {
        print " -> succeeded\n";        
        $result = "match";
        unlink "$testResultsDirectory/$base-actual.txt";
        unlink "$testResultsDirectory/$base-diffs.txt";
    } else {
        print " -> failed\n";        
        $result = "mismatch";
        my $dir = "$testResultsDirectory/$base";
        $dir =~ s|/[^/]+$||;
        system "mkdir", "-p", $dir;
        open ACTUAL, ">", "$testResultsDirectory/$base-actual.txt" or die;
        print ACTUAL $actual;
        close ACTUAL;
        system "diff -u \"$testDirectory/$base-expected.txt\" \"$testResultsDirectory/$base-actual.txt\" > \"$testResultsDirectory/$base-diffs.txt\"";
    }
    
    $count += 1;
    $counts{$result} += 1;
    push @{$tests{$result}}, $test;
}

close IN;
close OUT;

my %text = (
    match => "succeeded",
    mismatch => "had incorrect layout",
    new => "were new",
    fail => "failed (tool did not execute successfully)",
);

print "\n";

if ($counts{match} && $counts{match} == $count) {
    print "all $count test cases succeeded\n";
    unlink $testResults;
} else {
    for my $type ("match", "mismatch", "new", "fail") {
        my $c = $counts{$type};
        if ($c) {
            my $t = $text{$type};
            my $message;
            if ($c == 1) {
                $t =~ s/were/was/;
                $message = sprintf "1 test case (%d%%) %s\n", 1 * 100 / $count, $t;
            } else {
                $message = sprintf "%d test cases (%d%%) %s\n", $c, $c * 100 / $count, $t;
            }
            $message =~ s-\(0%\)-(<1%)-;
            print $message;
        }
    }
    
    system "mkdir", "-p", $testResultsDirectory;

    open HTML, ">", $testResults or die;
    print HTML "<html>\n";
    print HTML "<head>\n";
    print HTML "<title>Layout Test Results</title>\n";
    print HTML "</head>\n";
    print HTML "<body>\n";

    if ($counts{mismatch}) {
        print HTML "<p>Tests where results did not match expected results:</p>\n";
        print HTML "<table>\n";
        for my $test (@{$tests{mismatch}}) {
            my $base = $test;
            $base =~ s/\.(svg)$//;
            print HTML "<tr>\n";
            print HTML "<td><a href=\"$testDirectory/$test\">$base</a></td>\n";
            print HTML "<td><a href=\"$testDirectory/$base-expected.txt\">expected</a></td>\n";
            print HTML "<td><a href=\"$base-actual.txt\">actual</a></td>\n";
            print HTML "<td><a href=\"$base-diffs.txt\">diffs</a></td>\n";
            print HTML "</tr>\n";
        }
        print HTML "</table>\n";
    }

    if ($counts{fail}) {
        print HTML "<p>Tests that caused the DumpRenderTree tool to fail:</p>\n";
        print HTML "<table>\n";
        for my $test (@{$tests{fail}}) {
            my $base = $test;
            $base =~ s/\.(html|xml|xhtml)$//;
            print HTML "<tr>\n";
            print HTML "<td><a href=\"$testDirectory/$test\">$base</a></td>\n";
            print HTML "</tr>\n";
        }
        print HTML "</table>\n";
    }

    if ($counts{new}) {
        print HTML "<p>Tests that had no expected results (probably new):</p>\n";
        print HTML "<table>\n";
        for my $test (@{$tests{new}}) {
            my $base = $test;
            $base =~ s/\.(svg)$//;
            print HTML "<tr>\n";
            print HTML "<td><a href=\"$testDirectory/$test\">$base</a></td>\n";
            print HTML "<td><a href=\"$testDirectory/$base-expected.txt\">results</a></td>\n";
            print HTML "</tr>\n";
        }
        print HTML "</table>\n";
    }

    print HTML "</body>\n";
    print HTML "</html>\n";
    close HTML;
    
    system "open", $testResults;
}
