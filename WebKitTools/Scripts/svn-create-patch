#!/usr/bin/perl -w

# Copyright (C) 2005, 2006 Apple Computer, Inc.  All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
#
# 1.  Redistributions of source code must retain the above copyright
#     notice, this list of conditions and the following disclaimer. 
# 2.  Redistributions in binary form must reproduce the above copyright
#     notice, this list of conditions and the following disclaimer in the
#     documentation and/or other materials provided with the distribution. 
# 3.  Neither the name of Apple Computer, Inc. ("Apple") nor the names of
#     its contributors may be used to endorse or promote products derived
#     from this software without specific prior written permission. 
#
# THIS SOFTWARE IS PROVIDED BY APPLE AND ITS CONTRIBUTORS "AS IS" AND ANY
# EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL APPLE OR ITS CONTRIBUTORS BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
# ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
# THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

# Simplified and improved "svn diff" script for WebKit Open Source Project, used to make patches.

# Differences from standard "svn diff":
#
#   Uses the real diff, not svn's built-in diff.
#   Always passes "-p" to diff so it will try to include function names.
#   Handles binary files (encoded as a base64 chunk of text).
#
# Missing features:
#
#   Sort the diffs, since svn emits them in a seemingly-random order.
#   Handle moved files.

use strict;
use Cwd;
use Getopt::Long;
use Time::gmtime;
use File::stat;
use File::Spec;
use POSIX qw(:errno_h);
use Config;
use MIME::Base64;

my $startDir = getcwd();
my %paths;

# Create list of paths to diff.
if (!@ARGV) {
    $paths{"."} = 1;
} else {
    for my $file (@ARGV) {
        die "can't handle absolute paths like \"$file\"\n" if $file =~ m|^/|;
        die "can't handle empty string path\n" if $file eq "";
        die "can't handle path with ' in the name like \"$file\"\n" if $file =~ /'/; # ' (keep Xcode syntax highlighting happy)

        my $untouchedFile = $file;
        
        # Add a leading and trailing slash to simplify logic below.
        $file = "/$file/";

        # Remove repeated slashes.
        $file =~ s|//+|/|g;

        # Remove meaningless sequences involving ".".
        $file =~ s|/\./|/|g;

        # Remove meaningless sequences involving "..".
        $file =~ s|/[^./]/\.\./|/|g;
        $file =~ s|/[^/]+[^./]/\.\./|/|g;
        $file =~ s|/[^./][^/]+/\.\./|/|g;
        die "can't handle paths with .. like \"$untouchedFile\"\n" if $file =~ m|/\.\./|;

        # Remove the leading and trailing slash.
        $file =~ s|^/(.*)/$|$1|;

        $paths{$file} = 1;
    }
}

# Remove any paths that also have a parent listed.
for my $path (keys %paths) {
    my $parent = $path;
    while ($parent =~ s|/+[^/]+$||) {
        if ($paths{$parent}) {
            delete $paths{$path};
            last;
        }
    }
}

sub getDirAndBase
{
    my ($path) = @_;
    if (-d $path) {
        $path =~ s|/+$||;
        return ($path, ".");
    }
    return ($1, $2) if $path =~ m|^(.+)/([^/]+)$|;
    $path !~ m|/| or die "Could not parse path name $path.\n";
    return (".", $path);
}

# Function to generate a diff.
sub diff
{
    my ($path) = @_;
    my ($dir, $base) = getDirAndBase($path);
    my $errors = "";
    chdir $dir or die;
    open DIFF, "svn diff --diff-cmd diff -x -uNp '$base' |" or die;
    my $indexPath;
    my $binaryPath;
    while (<DIFF>) {
        if (/^Index: (.*)/) {
            # New patch just started
            $indexPath = $1;
            if ($dir ne ".") {
                $indexPath = "$dir/$indexPath";
                s/Index: .*/Index: $indexPath/;
            }
            # Output encoded binary contents of last patch before beginning of next patch
            outputBinaryContent(File::Spec->abs2rel($binaryPath, $dir)) if ($binaryPath);
            undef $binaryPath;
        }
        if ($indexPath) {
            # Fix paths on diff, ---, and +++ lines to match preceding Index: line.
            s/\S+$/$indexPath/ if /^diff/;
            s/^--- \S+/--- $indexPath/;
            s/^\+\+\+ \S+/+++ $indexPath/ && undef $indexPath;
        }
        if ($binaryPath) {
            # Fix path on "Property changes on:" line to match preceding Index: line.
            s/^(Property changes on:) \S+/$1 $indexPath/;
        }
        $binaryPath = $indexPath if (/^Cannot display: file marked as a binary type\.$/);
        print;
    }
    close DIFF;
    # Output encoded binary contents if the last patch was binary
    outputBinaryContent(File::Spec->abs2rel($binaryPath, $dir)) if ($binaryPath);
    chdir $startDir or die;
    print STDERR $errors;
}

# Outputs binary content as encoded text
sub outputBinaryContent
{
    my ($path) = @_;
    # Deletion
    return if (! -e $path);
    # Addition or Modification
    my $buffer;
    open BINARY, $path  or die;
    while (read(BINARY, $buffer, 60*57)) {
        print encode_base64($buffer);
    }
    close BINARY;
    print "\n";
}

# Generate the diff for each passed file or directory.
for my $path (sort keys %paths) {
    diff($path);
}
